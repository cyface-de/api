import com.github.spotbugs.snom.SpotBugsTask

/*
 * Copyright 2020-2023 Cyface GmbH
 *
 * This file is part of the Cyface API Library.
 *
 *  The Cyface API Library is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  The Cyface API Library is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with the Cyface API Library.  If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * The build gradle file for the Cyface API Library.
 *
 * @author Armin Schnabel
 * @version 1.1.0
 * @since 1.0.0
 */
buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id 'eclipse'
  id 'idea'
  //noinspection SpellCheckingInspection
  id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
  id "com.github.spotbugs" version "4.7.5" apply false
  // Plugin to display the Gradle task graph
  //noinspection SpellCheckingInspection
  id 'org.barfuin.gradle.taskinfo' version '2.1.0'
}

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'
  apply plugin: 'java'
  apply plugin: 'java-library'

  group = 'de.cyface'
  version = "0.0.0" // Automatically overwritten by CI

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    checkstyle {
      toolVersion = '8.31'
      // use one common config file for all subprojects
      configFile = project(':').file("config/checkstyle/checkstyle.xml")
      //noinspection SpellCheckingInspection
      configProperties = ["suppressionFile": project(':').file("config/checkstyle/suppressions.xml")]
      ignoreFailures = true
      showViolations = true
    }
  }

  ext {
    // If you increase this version, check if the `collector.GridFSStorageIT` breaks
    // There is a bug up to version 4.4.1, scheduled to be fixed in 4.4.2
    // https://github.com/vert-x3/vertx-mongo-client/issues/291
    vertxVersion = '4.3.6'
    slf4jVersion = '2.0.7'
    commonsLangVersion = '3.12.0'
    gradleWrapperVersion = '7.6.1'

    // Versions of testing dependencies
    junitVersion = '5.9.2'
    mockitoVersion = '5.2.0'
    flapdoodleVersion = '3.5.3' // major upgrade available

    jacocoVersion = '0.8.9'
    spotBugsPluginVersion = '1.12.0'
    spotbugsToolsVersion = '4.7.3'
    pmdToolsVersion = '6.55.0'
  }

  wrapper {
    gradleVersion = "$gradleWrapperVersion"
  }

  dependencies {
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.apache.commons:commons-lang3:$commonsLangVersion"

    // Testing Dependencies
    testImplementation(platform("org.junit:junit-bom:$junitVersion"))
    testImplementation "org.junit.jupiter:junit-jupiter-params"  // Required for parameterized tests
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:$spotBugsPluginVersion"
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "passed", "skipped", "failed"

      // Also show assert message (e.g. on the CI) when tests fail to identify cause
      showExceptions true
      exceptionFormat "full"
      showCauses true
      showStackTraces true
      showStandardStreams = false
    }
  }

  jacoco {
    toolVersion = "$jacocoVersion"
    reportsDir = file("$buildDir/reports/jacoco")
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      csv.enabled true
      html.destination file("${buildDir}/reports/jacocoHtml")
    }
  }

  spotbugs {
    toolVersion = "$spotbugsToolsVersion"
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
  }

  tasks.withType(SpotBugsTask) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
    //pluginClasspath = project.configurations.spotbugsPlugins
  }

  pmd {
    toolVersion = "$pmdToolsVersion"
    incrementalAnalysis = true
    ruleSetFiles = project(':').files('config/pmd.xml')
    rulesMinimumPriority = 4
    ruleSets = []
    // There are so many violations and currently it is not really important in this application.
    ignoreFailures = true
  }

  // Definitions for the maven-publish Plugin
  publishing {
    // The following repositories are used to publish artifacts to.
    repositories {
      maven {
        name = 'github'
        url = uri("https://maven.pkg.github.com/cyface-de/api")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
        }
      }
      maven {
        name = 'local'
        url = "file://${rootProject.buildDir}/repo"
      }
    }
  }

  // The following repositories are used to load artifacts from.
  repositories {
    maven {
      name = 'local'
      url = "file://${rootProject.buildDir}/repo"
    }
    maven {
      name = "github"
      url = uri("https://maven.pkg.github.com/cyface-de/serializer")
      credentials {
        username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
      }
    }
  }

  // The following needs to be wrapped in afterEvaluate. I have no idea why, but the solution is described here: https://github.com/akhikhl/gretty/issues/306
  project.afterEvaluate {

    java {
      withJavadocJar()
      withSourcesJar()
    }

    // Publish sources
    task sourceJar(type: Jar) {
      from sourceSets.main.allJava
    }

    // This is the configuration of the maven-publish plugin. It defines a publication
    publishing {
      publications {
        //noinspection GroovyAssignabilityCheck
        myLibrary(MavenPublication) {
          //noinspection GroovyAssignabilityCheck
          from components.java
        }
      }
    }
  }
}
